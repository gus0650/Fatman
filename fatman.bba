;fatman
;blitzbasic 2.1 amiga
;v1.0 14.3.2025

;shape constants
#HEIKO=0
#HEIKO1=1
#LOGO=2
#GUN=3
#MUZZLE=4

#SND_START=0
#SND_BOUNCE=1
#SND_GUN=2
#SND_KILL=3
#SND_FAIL=4
;sound channels = 1 2 4 8

;music
LoadModule 0, "assets/mus/boing.mod"

LoadSound #SND_START, "assets/snd/trringg!.iff"
LoadSound #SND_BOUNCE, "assets/snd/bounce.8svx.iff"
LoadSound #SND_GUN, "assets/snd/gun.8svx.iff"
LoadSound #SND_KILL, "assets/snd/die.8svx.iff"
LoadSound #SND_FAIL, "assets/snd/bottieburp.iff"

;doublebuffer
BitMap 0,320,256,5
BitMap 1,320,256,5

LoadBitMap 0, "assets/gfx/fatman.pic.32.iff", 0
LoadPalette 0, "assets/gfx/fatman.pic.32.iff", 0

;extract shapes
use bitmap 0

for i = 0 to 4
    read x, y, x2, y2
    w=x2-x
    h=y2-y

    GetaShape i,x,y,w,h
next

;coordinates of left top corner + bottom right corner
DATA 7,97,156,246    ;heiko1
DATA 157,97,232,158	;heiko2
DATA 50,15,201,90	 ;logo
DATA 213,20,246,49 ;gun
DATA 188,180,226,249 ;muzzle

Boxf 6,12,246,250,0 ;clear play area

CopyBitMap 0, 1 ;copy to second buffer

;---------------------- SETUP DISPLAY

BLITZ

Slice 0,44,5
Show 0
use palette 0

BitMapInput

Mouse off

;---------------------- TITLE

title:

PlayModule 0

use bitmap 0
Boxf 6,12,246,250,0 ;clear play area
Boxf 252,30,310,200,0 ;clear text area

blit #LOGO, 60, 60

BitMapOutput 0
Locate 33,4
Print "PRESS"
Locate 33,5
Print "FIRE"
Locate 33,6
Print "TO"
Locate 33,7
Print "START"

Locate 7,27
Print "(c)1990 G Stechmann"
Locate 6,28
Print "Music by Yannis Brown"

show 0

VWait 5; wait for joystick button release

while TRUE
  if Joyb(1) goto main
  VWait
wend


;---------------------- MAIN LOOP

main:

Boxf 252,30,310,200,0 ;clear text area

StopModule

VWait 5 ;wait for release mouse button

Sound #SND_START, 15

heiko=50
bounce = 150
y=100
db=0
ammo = 6
muzzle = 0

;bounce
while TRUE
  db=1-db ;switch to offscreen bitmap
  use bitmap db

  Boxf 6,12,246,250,0 ;clear play area
  Boxf 252,30,310,200,0 ;clear text area

  ;draw ammo
  BitMapOutput db
  Locate 33,0.9
  print "0000", ammo

  ;draw gun
  blit #GUN, 200, 20

  if (muzzle > 0) blit #MUZZLE, 160, 7
  muzzle = muzzle-1

  ;calculate movement
  bounce=bounce+2
  IF bounce=180 ;reached bottom
  	bounce=2
    Sound #SND_BOUNCE, 1
  ENDIF

  x=SIN(bounce*3.141593/180)
  heiko=INT((x*(y-244)/2.3)+107)-30

  ;draw heiko
  BlitMode CookieMode
  blit #HEIKO, 50, heiko

  ;fire button pressed?
  bitmapinput
  if Joyb(1)
    Sound #SND_GUN, 2

    if heiko<26
      ;treffer
      blit #HEIKO1, 59, heiko

      blit #MUZZLE, 160, 6

      goto win
    endif

    ammo = ammo-1
    if ammo = 0 goto loose

    muzzle = 4; show for n frames
  endif

  show db
  
  VWait

wend


;---------------------- LOOSE

loose:

Sound #SND_FAIL, 4

Locate 33,0.9
print "00000"

Locate 33,4
print "OUT"
Locate 33,5
print "OF"
Locate 33,6
print "AMMO"

Locate 33,8
print "YOU"
Locate 33,9
print "FAIL"
Locate 33,10
print "!!!"

show db
VWait 50

while TRUE
  if Joyb(1) goto title
  VWait
wend


;---------------------- WIN

win:

Sound #SND_KILL, 4

Locate 33,4
print "NICE"
Locate 33,5
print "SHOT"

Locate 33,7
print "YOU"
Locate 33,8
print "WIN"
Locate 33,9
print "!!!"

show db
VWait 50

while TRUE
  if Joyb(1) goto title
  VWait
wend
